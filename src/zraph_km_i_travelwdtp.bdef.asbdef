managed implementation in class zraph_bp_km_i_travelwdtp unique;
with draft;

define behavior for ZRAPH_KM_I_TravelWDTP alias Travel
persistent table zraph_km_travel
draft table zkm_d_travel
lock master
total etag LastChangedAt
//authorization master (instance)
etag master LocalLastChangedAt
{
  create;
  update;
  delete;
  association _Booking { create (features:instance) ;with draft; }
  association _RoomReservation { create; with draft; }

  field ( numbering : managed, readonly ) TravelUUID;
  field (mandatory) BeginDate, EndDate;
  field(readonly) OverallStatus, TotalPrice, TravelID;
  field (features : instance) BookingFee;

  determination setInitialStatus on modify { create; }
  determination calculateTotalPrice on modify { field BookingFee, CurrencyCode; }
  determination setTravelID on modify { create; }

  validation validateCustomer on save { create; field CustomerID; }
  validation validateAgency on save { create; field AgencyID; }
  validation validateDates on save { field BeginDate, EndDate; create; }

  action (features : instance) acceptTravel result [1] $self;
  action (features : instance) rejectTravel result [1] $self;
  internal action reCalcTotalPrice;

  draft determine action Prepare
  {
    validation validateCustomer;
    validation validateDates;
    validation validateAgency;
    validation Booking ~ validateCustomer;
    validation BookingSupplement ~ validateSupplement;
  }

  mapping for zraph_km_travel
  {
    TravelUUID = travel_uuid;
    TravelID = travel_id;
    AgencyID = agency_id;
    CustomerID = customer_id;
    BeginDate = begin_date;
    EndDate = end_date;
    BookingFee = booking_fee;
    TotalPrice = total_price;
    CurrencyCode = currency_code;
    Description = description;
    OverallStatus = overall_status;
    LocalCreatedBy = local_created_by;
    LocalCreatedAt = local_created_at;
    LocalLastChangedBy = local_last_changed_by;
    LastChangedAt = last_changed_at;
    LocalLastChangedAt = local_last_changed_at;
  }
}

define behavior for ZRAPH_KM_I_BookingWDTP alias Booking
persistent table zraph_km_booking
draft table zkm_d_booking
lock dependent by _Travel
etag master LocalLastChangedAt
{
  update;
  delete;
  field ( numbering : managed, readonly ) BookingUUID;
  field ( readonly ) TravelUUID;

  determination calculateTotalPrice on modify { field flightprice, CurrencyCode; }
  determination setBookingDate on modify { create; }

  validation validateCustomer on save { create; field CustomerID; }

  association _Travel { with draft; }
  association _BookingSupplement { create; with draft; }

  mapping for ZRAPH_KM_Booking
  {
    BookingUUID = booking_uuid;
    TravelUUID = parent_uuid;
    BookingID = booking_id;
    BookingDate = booking_date;
    CustomerID = customer_id;
    CarrierID = carrier_id;
    ConnectionID = connection_id;
    FlightDate = flight_date;
    FlightPrice = flight_price;
    CurrencyCode = currency_code;
    LocalLastChangedAt = local_last_changed_at;
  }
}

define behavior for ZRAPH_KM_I_BookingSupplWDTP alias BookingSupplement
persistent table zraph_km_booksup
draft table zkm_d_bksup
lock dependent by _Travel
etag master LocalLastChangedAt
{
  update;
  delete;
  field ( numbering : managed, readonly ) BookSupplUuid;
  field ( readonly ) TravelUUID, BookingUUID;

  determination calculateTotalPrice on modify { field price, CurrencyCode; }

  association _Travel { with draft; }
  association _Booking { with draft; }

  validation validateSupplement on save { create; field supplementID; }

  mapping for ZRAPH_KM_BookSup
  {
    BookSupplUUID = booksuppl_uuid;
    TravelUUID = root_uuid;
    BookingUUID = parent_uuid;
    BookingSupplementID = booking_supplement_id;
    SupplementID = supplement_id;
    Price = price;
    CurrencyCode = currency_code;
    LocalLastChangedAt = local_last_changed_at;
  }
}

define behavior for ZRAPH_KM_I_RoomReservationWDTP alias RoomReservation
persistent table zraph_km_roomrsv
draft table zkm_d_roomrsv
lock dependent by _Travel
etag master LocalLastChangedAt
{
  update;
  delete;

  determination calculateTotalPrice on modify { field RoomRsvPrice, CurrencyCode; }

  field ( numbering : managed, readonly ) RoomrsvUuid;
  field ( readonly ) TravelUuid;

  association _Travel { with draft; }

  mapping for ZRAPH_KM_RoomRsv
  {
    RoomRsvUUID = roomrsv_uuid;
    TravelUUID = parent_uuid;
    RoomRsvID = roomrsv_id;
    HotelID = hotel_id;
    BeginDate = begin_date;
    EndDate = end_date;
    RoomType = room_type;
    RoomRsvPrice = roomrsv_price;
    CurrencyCode = currency_code;
    LocalLastChangedAt = local_last_changed_at;
  }
}